import org.gradle.internal.os.OperatingSystem
plugins {
    id 'com.ullink.msbuild' version '2.15'
    id 'com.ullink.nunit' version '1.12'
    id 'com.ullink.nuget' version '2.12'
}

def nativeLibDir = "$buildDir/native_libs"

configurations {
    nativeLib
}

nuget {
    version = '4.4.1'
}

msbuild {
    solutionFile = file('recast-csharp.sln')
    configuration = 'Release'
    projectName = 'Recast'
    inputs.file(project.buildFile)
    intermediateDir = 'build/msbuild/obj'
}

nunit {
    testAssemblies = [file("$buildDir/msbuild/bin/Release/Recast.Tests.dll")]
}

msbuild.dependsOn(nugetRestore)
nunit.dependsOn(msbuild)

assemble.dependsOn(msbuild)
check.dependsOn(nunit)

dependencies {
    nativeLib project(path: ':recast', configuration: 'releaseLinkElements')
}

repositories {
    mavenCentral()
}

task darwinNativeLibs(type: Sync) {
    dependsOn configurations.nativeLib
    from configurations.nativeLib.filter { it.path.endsWith(".dylib") || it.path.endsWith(".so") }
    into "${nativeLibDir}/darwin"
}

version = "0.0.1"

def nuspecMetadata = [
    version: version,
    title: 'Recast Wrapper',
    authors: 'Improbable',
    owners: 'Improbable',
    projectUrl: 'https://github.com/improbable/recast-wrapper',
    requireLicenseAcceptance: false,
    dependencies: []
]

// See https://stackoverflow.com/questions/40104838/automatic-native-and-managed-dlls-extracting-from-nuget-package/40652794#40652794
// and https://stackoverflow.com/questions/10198428/where-to-place-dlls-for-unmanaged-libraries
// and https://www.nuget.org/packages/Grpc.Core/1.12.0
// for how to package native libraries.
nugetSpec {
    nuspec = [
        metadata: nuspecMetadata,
        files: [
            { file (src: "${buildDir}/msbuild/bin/Release/Recast.dll", target: 'lib/net45') },
            { file (src: "${buildDir}/msbuild/bin/Release/Recast.pdb", target: 'lib/net45') },
            { file (src: "${buildDir}/msbuild/bin/Release/librecastwrapper.dylib", target: 'runtimes/osx/native/') },
            // { file (src: "${buildDir}/msbuild/bin/Release/librecastwrapper.so", target: 'lib/') },
            { file (src: "${projectDir}/Recast.targets", target: 'build/net45') },
        ]
    ]
}

nugetPush {
    // TODO: changeme
    serverUrl = "file://$projectDir/local_publish"
    // apiKey = project.properties.nugetApiKey
    nupkgFile = nugetPack.packageFile
}

nugetPack.dependsOn msbuild

tasks.matching { it.name != 'darwinNativeLibs' && it.name != 'clean' && it.name != 'cleanMsbuild' }.all { task ->
    task.dependsOn(darwinNativeLibs)
}
