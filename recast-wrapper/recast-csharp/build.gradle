plugins {
    id 'com.ullink.msbuild' version '2.18'
    id 'com.ullink.nunit' version '1.12'
    id 'com.ullink.nuget' version '2.12'
}

apply from: "$rootDir/gradle/native_libs.gradle"

nuget {
    version = '4.4.1'
}

msbuild {
    solutionFile = file('recast-csharp.sln')
    configuration = 'Release'
    projectName = 'Improbable.Recast'
    inputs.file(project.buildFile)
    intermediateDir = 'build/msbuild/obj'
    msbuildDir = System.getenv('MSBUILD_DIR')
}

nunit {
    testAssemblies = [file("$buildDir/msbuild/bin/Release/Improbable.Recast.Tests.dll")]
    nunitVersion = '3.8.0'
}

msbuild.dependsOn(nugetRestore)
nunit.dependsOn(msbuild)

assemble.dependsOn(msbuild)
check.dependsOn(nunit)

dependencies {
    // Needs to be intransitive as we don't want the underlying recast library files (they get linked into recast-wrapper)
    nativeLib(project(path: ':recast-wrapper', configuration: "debugLinkElements")) { transitive = false }
}

repositories {
    mavenCentral()
}

tasks.matching { it.name != 'darwinNativeLibs' && it.name != 'unixNativeLibs' && it.name != 'windowsNativeLibs' && it.name != 'clean' && it.name != 'cleanMsbuild' }.all { task ->
    task.dependsOn(darwinNativeLibs)
    task.dependsOn(unixNativeLibs)
    task.dependsOn(windowsNativeLibs)
}
