plugins {
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.2.21"
}

def nativeLibDir = "$buildDir/native_libs"

configurations {
    nativeLib
}

dependencies {
    nativeLib project(path: ':recast', configuration: 'releaseLinkElements')
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "net.java.dev.jna:jna:4.5.1"
    testImplementation "junit:junit:4.12"
    testCompile "com.natpryce:hamkrest:1.4.2.2"
}

repositories {
    mavenCentral()
}

task darwinNativeLibs(type: Sync) {
    dependsOn configurations.nativeLib
    from configurations.nativeLib.filter { it.path.endsWith(".dylib") }
    into "${nativeLibDir}/darwin"
}

task unixNativeLibs(type: Sync) {
    dependsOn configurations.nativeLib
    from configurations.nativeLib.filter { it.path.endsWith(".so") }
    into "${nativeLibDir}/linux-x86-64"
}

tasks.matching { it.name != 'darwinNativeLibs' && it.name != 'unixNativeLibs' && it.name != 'clean' }.all { task ->
    task.dependsOn(darwinNativeLibs)
    task.dependsOn(unixNativeLibs)
}

sourceSets { main { resources { srcDir nativeLibDir } } }
